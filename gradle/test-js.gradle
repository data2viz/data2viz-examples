// Configures testing for JS modules

apply plugin: 'com.moowork.node'

task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir
    into "${buildDir}/node_modules"

    afterEvaluate {
        configurations.testCompile.each {
            from zipTree(it.absolutePath).matching {
                include '*.js'
                include '*.js.map'
            }
        }
    }
}

node {
    version = "$node_version"
    npmVersion = "$npm_version"
    download = true
}

task installDependencies(type: YarnTask) {
    args = ['install']
}

task prepareMocha(dependsOn: [compileTestKotlin2Js, populateNodeModules, installDependencies])

task runJsTests(type: YarnTask, dependsOn: prepareMocha) {
    args = ['test']}

test.dependsOn runJsTests